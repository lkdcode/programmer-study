import dev.lkdcode.yml.YmlParser


def ymlParser = new YmlParser(project)

def jdbcDriver = ymlParser.getFlywayDriver()
def jdbcUrl = ymlParser.getFlywayUrl()
def jdbcUser = ymlParser.getFlywayUser()
def jdbcPass = ymlParser.getFlywayPassword()

dependencies {
    jooqGenerator(project(":jooq-config"))
}

jooq {
    version = JooqLibs.JOOQ_VERSION

    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            jooqConfiguration.with {
                jdbc.with {
                    driver = jdbcDriver
                    url = jdbcUrl
                    user = jdbcUser
                    password = jdbcPass
                }

                generator.with {
                    name = 'org.jooq.codegen.KotlinGenerator'

                    strategy.name = JooqLibs.STRATEGY_NAME

                    database.with {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        excludes = 'flyway_schema_history'
                    }

                    generate.with {
                        javaTimeTypes = true
                        deprecated = false
                        fluentSetters = true
                        records = true
                        daos = false

                        kotlinNotNullPojoAttributes = true
                        kotlinNotNullRecordAttributes = true
                        kotlinNotNullInterfaceAttributes = true
                    }

                    target.with {
                        packageName = JooqLibs.TARGET_PACKAGE_NAME
                        directory = layout.buildDirectory.dir(JooqLibs.DEFAULT_DIRECTORY).get().asFile.path
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.dir(JooqLibs.DEFAULT_DIRECTORY).get().asFile.path
        }
    }
}