import org.yaml.snakeyaml.Yaml


def profile = (findProperty('spring.profiles.active') as String) ?: 'local'
def ymlFile = file("src/main/resources/application-${profile}.yml")

Map yml = (new Yaml().load(ymlFile.text) as Map) ?: [:]

def nav = { Map m, Object... keys ->
    def cur = m
    for (k in keys) {
        if (!(cur instanceof Map)) return null
        cur = cur[k as String]
        if (cur == null) return null
    }
    return cur
}

def jdbcUrl = String.valueOf(nav(yml, 'spring', 'flyway', 'url'))
def jdbcUser = String.valueOf(nav(yml, 'spring', 'r2dbc', 'username'))
def jdbcPass = String.valueOf(nav(yml, 'spring', 'r2dbc', 'password'))

flyway {
    driver = "org.postgresql.Driver"
    url = jdbcUrl
    user = jdbcUser
    password = jdbcPass
    defaultSchema = "public"
    locations = ['classpath:db/migration']
}